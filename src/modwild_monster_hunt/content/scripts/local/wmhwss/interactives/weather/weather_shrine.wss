/// There is a vanilla WeatherShrine class, however it doesn't 100% cover our
/// needs: A shrine can only change the weather type to 1 specific value.
statemachine class WMH_WeatherShrine extends CGameplayEntity {
  protected editable var target_weather: WMH_WeatherType;
  hint target_weather = "The desired weather after the shrine is interacted with.";

  private var oneliner: WMH_GameplayOnelinerEntity;

  public editable var quickstart_options: WMH_QuickStartLocationOptions;

  event OnInteraction(actionName: string, activator: CEntity) {
    this.GotoState('StartDialogChoice');
  }

  event OnInteractionActivationTest(
    interactionComponentName: string,
    activator: CEntity
  ) {
    this.displayTutorial();

    return true;
  }

  event OnInteractionActivated(
    interactionComponentName: string,
    activator: CEntity
  ) {
    if (activator != thePlayer) {
      return false;
    }

    if (!this.oneliner) {
      this.setupOneliner();
    }
    else {
      this.oneliner.register();
    }
  }

  event OnInteractionDeactivated(
    interactionComponentName: string,
    activator: CEntity
  ) {
    if (activator != thePlayer) {
      return false;
    }

    if (this.oneliner) {
      this.oneliner.unregister();
    }
  }

  protected function changeWeather(target_weather: WMH_WeatherType) {
    var current: WMH_WeatherType = WMH_getWeatherType();
    var target_weather_name: name;

    switch (target_weather) {
      case WMH_WeatherType_Clear:
      case WMH_WeatherType_None:
        target_weather_name = 'WT_Clear';
        break;

      case WMH_WeatherType_Snow:
        target_weather_name = 'WT_Snow';
        break;

      case WMH_WeatherType_Rain:
        target_weather_name = 'WT_Rain_Storm';
        break;
    }

    var success: bool = RequestWeatherChangeTo(target_weather_name, 60, false);
    if (success) {
      thePlayer.DisplayHudMessage(
        GetLocStringByKeyExt("panel_hud_message_prayer_heard")
      );

      theSound.SoundEvent("gui_character_synergy_effect");
    }
    else {
      thePlayer.DisplayHudMessage(
        GetLocStringByKeyExt("panel_hud_message_prayer_not_heard")
      );

      theSound.SoundEvent("gui_character_synergy_effect_lose");
    }
  }

  private function displayTutorial() {
    WMHTUTOFACT(
      'WMH_WeatherShrine',
      GetLocStringByKeyExt("wmh_tutorial_weather_shrine_title"),
      GetLocStringByKeyExt("wmh_tutorial_weather_shrine_description")
    );
  }

  protected function setupOneliner() {
    if (this.oneliner) {
      this.oneliner.unregister();
    }

    var activated_message: string = GetLocStringByKeyExt("wmh_inactive_uppercase");
    if (WMH_isQuickStartLocationActivated(this.quickstart_options)) {
      activated_message = "<font color='#CD7D03'>"
        +GetLocStringByKeyExt("wmh_active_uppercase")
        +"</font>";
    }

    this.oneliner = WMH_gameplayOnelinerEntity(
      GetLocStringByKeyExt("wmh_quickstart_oneliner") + "<br/>" + activated_message,
      this
    );

    this.oneliner.offset = Vector(0, 0, 2);
    this.oneliner.visible_outside_focus_mode = true;
  }
}

state Waiting in WMH_WeatherShrine {}

state StartDialogChoice in WMH_WeatherShrine {
  event OnEnterState(previous_state_name: name) {
    super.OnEnterState(previous_state_name);
    parent.GotoState('DialogChoice');
  }
}

state DialogChoice in WMH_WeatherShrine {
  event OnEnterState(previous_state_name: name) {
    super.OnEnterState(previous_state_name);
    this.DialogChoice_main();
  }

  entry function DialogChoice_main() {
    this.displayMainChoices();
    parent.GotoState('Waiting');
  }

  latent function displayMainChoices() {
    var choices: array<SSceneChoice>;

    while (true) {
      choices.Clear();

      var upkeep_cost: int = WMH_getQuickStartMerchantGoodsCost(
        parent.quickstart_options
      );

      if (WMH_isQuickStartLocationActivated(parent.quickstart_options)) {
        choices.PushBack(SSceneChoice(
          StrReplace(
            GetLocStringByKeyExt("wmh_quickstart_dialog_deactivate"),
            "{{cost}}",
            upkeep_cost
          ),
          true, // true
          false, // previouslyChoosen
          false, // disabled
          DialogAction_GETBACK,
          'QuickStartDeactivate'
        ));
      }
      else {
        choices.PushBack(SSceneChoice(
          StrReplace(
            GetLocStringByKeyExt("wmh_quickstart_dialog_activate"),
            "{{cost}}",
            upkeep_cost
          ),
          false, // true
          true, // previouslyChoosen
          false, // disabled
          DialogAction_FAST_TRAVEL,
          'QuickStartActivate'
        ));
      }

      if (parent.target_weather == WMH_WeatherType_Rain) {
        choices.PushBack(SSceneChoice(
          GetLocStringByKeyExt("wmh_weather_shrine_summon_rain"),
          false, // emphasis
          false, // previouslyChoosen
          false, // disabled
          DialogAction_NONE,
          'WeatherSummonRain'
        ));
      }

      if (
        parent.target_weather == WMH_WeatherType_Clear
        || parent.target_weather == WMH_WeatherType_None
      ) {
        choices.PushBack(SSceneChoice(
          GetLocStringByKeyExt("wmh_weather_shrine_summon_clear"),
          false, // emphasis
          false, // previouslyChoosen
          false, // disabled
          DialogAction_NONE,
          'WeatherSummonClear'
        ));
      }

      if (parent.target_weather == WMH_WeatherType_Snow) {
        choices.PushBack(SSceneChoice(
          GetLocStringByKeyExt("wmh_weather_shrine_summon_snow"),
          false, // emphasis
          false, // previouslyChoosen
          false, // disabled
          DialogAction_NONE,
          'WeatherSummonSnow'
        ));
      }

      choices.PushBack(SSceneChoice(
        GetLocStringByKeyExt("wmh_cancel"),
        false, // emphasis
        false, // previouslyChoosen
        false, // disabled
        DialogAction_EXIT,
        'Cancel'
      ));

      var response: SSceneChoice = SU_setDialogChoicesAndWaitForResponse(choices);
      SU_closeDialogChoiceInterface();

      if (response.playGoChunk == 'QuickStartDeactivate') {
        WMH_toggleQuickStartLocation(parent.quickstart_options, false);
        theSound.SoundEvent("gui_ingame_wheel_close");
        parent.setupOneliner();
      }
      else if (response.playGoChunk == 'QuickStartActivate') {
        WMH_toggleQuickStartLocation(parent.quickstart_options, true);
        theSound.SoundEvent("gui_ingame_wheel_open");
        parent.setupOneliner();
      }
      else if (response.playGoChunk == 'WeatherSummonClear') {
        parent.changeWeather(WMH_WeatherType_Clear);
        return;
      }
      else if (response.playGoChunk == 'WeatherSummonRain') {
        parent.changeWeather(WMH_WeatherType_Rain);
        return;
      }
      else if (response.playGoChunk == 'WeatherSummonSnow') {
        parent.changeWeather(WMH_WeatherType_Snow);
        return;
      }
      else {
        return;
      }
    }

    return;
  }
}