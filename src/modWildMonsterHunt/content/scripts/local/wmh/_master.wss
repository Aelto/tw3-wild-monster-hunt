class WMH_Master {
  public var storage: WMH_Storage;
  public var logger: WMH_Logger;

  public var camp: WMH_CampManager;
  public var bestiary: WMH_Bestiary;
  public var hunt: WMH_HuntManager;
  public var loot: WMH_LootManager;

  public function onCreate() {}

  public function onLoad() {
    this.storage = (new WMH_Storage in this).init();
    this.logger = (new WMH_Logger in this).init();
    WMHINFO("WMH_Master::OnLoad()");

    this.camp = (new WMH_CampManager in this).init(this);
    this.bestiary = (new WMH_Bestiary in this).init(this);
    this.hunt = (new WMH_HuntManager in this).init(this);
    this.loot = (new WMH_LootManager in this).init();
    
    // var a: int = "compile_fail";
  }

  public function submitOnSettlementEnter(
    origin: WMH_SettlementAreaTrigger
  ) {
    this.hunt.onHuntFinished(this.bestiary, this.storage);
    this.loot.onHuntFinished(this.storage);
  }

  public function submitOnSettlementExit(
    origin: WMH_SettlementAreaTrigger
  ) {
    this.hunt.onHuntStarted(this.storage);
    this.hunt.contract.onHuntStarted(this.bestiary);
    this.bestiary.spawn_points.onHuntStarted();
  }

  public function submitOnSpawn(
    origin: WMH_HuntManager,
    contract: WMH_ContractManager,
    hunt_seed: int,
    instant_seed: int
  ) {
    this.hunt.contract.onSpawn(bestiary, hunt_seed);
    this.bestiary.onSpawn(contract, hunt_seed);
  }

  public function submitOnSpawnContractTarget(
    origin: WMH_PendingContract,
    bentry: WMH_BestiaryEntry,
    location: WMH_BiomeSpawnPoint,
    seed: int
  ) {
    this.bestiary.onSpawnContractTarget(
      bentry,
      location,
      origin.affix_manager,
      seed
    );
  }

  public function submitOnSpawnClues(
    origin: WMH_PendingContract,
    bentry: WMH_BestiaryEntry,
    location: WMH_BiomeSpawnPoint,
    clues_heading: float,
    seed: int
  ) {
    this.bestiary.onSpawnClues(bentry, location, clues_heading, seed);
  }

  public function submitOnEncounterSpawned(
    origin: WMH_SpawnRequest,
    spawn_point: WMH_BiomeSpawnPoint,
    entries: array<WMH_BestiaryEntry>,
    entities: array<CEntity>
  ) {
    var encounter: WMH_Encounter = (new WMH_Encounter in thePlayer.wmh)
      .init(spawn_point, entries, entities);

    this.hunt.encounters.onEncounterSpawned(encounter);
  }

  public function submitOnCluesSpawned(
    origin: WMH_SpawnRequestClues,
    spawn_point: WMH_BiomeSpawnPoint,
    entries: array<WMH_BestiaryEntry>,
    entities: array<CEntity>
  ) {
    var encounter: WMH_EncounterClues = (new WMH_EncounterClues in thePlayer.wmh)
      .initClues(spawn_point, entries, entities);

    this.hunt.encounters.onCluesSpawned(encounter);
  }

  public function submitOnCreatureKilled(
    origin: WMH_Encounter,
    bentry: WMH_BestiaryEntry
  ) {
    WMHINFO("submitOnCreatureKilled: id=" + bentry.id.value);

    this.hunt.onCreatureKilled(bentry);
  }

  public function submitOnContainerRefill(
    origin: WMH_RefillableContainer,
    inventory: CInventoryComponent,
    seed: int
  ) {
    this.loot.onContainerRefill(inventory, origin.loot_tags, seed);
  }

  public function submitOnDeath(origin: CR4PlayerStateUnconscious) {
    // 1. teleport player to feint respawn location
    var destination: CEntity = theGame.GetEntityByTag('WMH_FeintRespawn');

    this.hunt.onPlayerDeath();

    if (destination) {
      thePlayer.TeleportWithRotation(
        destination.GetWorldPosition(),
        destination.GetWorldRotation()
      );
    }

    // 2. break some consumables
    var inventory: CInventoryComponent = thePlayer.GetInventory();
    var consumables: array<SItemUniqueId> = inventory.GetItemsByTag('Potion');
    var oils: array<SItemUniqueId> = inventory.GetItemsByTag('SilverOil');

    // join the two arrays
    for uuid: SItemUniqueId in oils {
      consumables.PushBack(uuid);
    }

    var level: int = this.hunt.difficulty();
    var seed: int = this.hunt.getSeed();
    // 40% lost at level 25, with a maximum of 80% at higher levels
    var max_percent: float = MaxF(0.8, (level / 25) * 0.4);

    var items_to_break: int = (
      consumables.Size()
      * RandNoiseF(seed,max_percent)
    ) as int;

    while (items_to_break > 0) {
      items_to_break -= 1;

      var index: int = RandNoiseF(
        seed + items_to_break,
        consumables.Size()
      ) as int;

      var item: SItemUniqueId = consumables[index];
      var witcherplayer: W3PlayerWitcher = GetWitcherPlayer();

      consumables.EraseFast(index);

      if (witcherplayer.IsItemEquipped(item)) {
        witcherplayer.UnequipItem(item);
      }

      inventory.SingletonItemRemoveAmmo(item, 1);
    }
  }

  public function submitOnMilestoneContractCompleted(
    origin: WMH_MilestoneContract,
    seed: int
  ) {
    this.loot.onMilestoneContractCompleted(seed);
  }
}

