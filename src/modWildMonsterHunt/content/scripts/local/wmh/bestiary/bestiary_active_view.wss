// Acts as a dynamic view (like in databases) of the list of active bestiary
// entries. A bestiary entry can be considered active or inactive depending on
// the time of day, or the surrounding biomes for example.
//
// As the player moves around the maps the view is adjusted to match the new
// data.
class WMH_BestiaryActiveView {
  private var entries: array<WMH_BestiaryEntry>;
  private var refresh_ticker: WMH_Ticker;

  public function init(): WMH_BestiaryActiveView {
    this.refresh_ticker = (new WMH_Ticker in this).init(60); // seconds

    return this;
  }

  public function maybeRefresh(
    entries: array<WMH_BestiaryEntry>,
    biome_manager: WMH_BiomeManager
  ) {
    if (!this.refresh_ticker.validate()) {
      return;
    }

    this.updateView(entries, biome_manager);
  }

  private function updateView(
    entries: array<WMH_BestiaryEntry>,
    biome_manager: WMH_BiomeManager
  ) {
    var weather: WMH_WeatherType = biome_manager.getWeatherType();
    var biome: WMH_BiomeTags = biome_manager.getGlobalBiome();
    var day_hour: int = biome_manager.getDayHour();
    var current: WMH_BestiaryEntry;

    this.entries.Clear();

    // build a list with just the entries that we allow to spawn
    for current: WMH_BestiaryEntry in entries {
      // this is where additional filtering can be done, for example based on
      // the player's progression through the game or if the species are in
      // the player's bestiary.

      this.entries.PushBack(current);
    }
  }

  public function getRandomEntry(
    biome_manager: WMH_BiomeManager,
    biome_tags: WMH_BiomeTags,
    biome_tags_bypass: array<name>,
    seed: int
  ): WMH_BestiaryEntry {
    var selected_entries: array<WMH_BestiaryEntry>;

    var weather: WMH_WeatherType = biome_manager.getWeatherType();
    var day_hour: int = biome_manager.getDayHour();
    var global_biome: WMH_BiomeTags = biome_manager.getGlobalBiome();

    var current: WMH_BestiaryEntry;

    // 1.
    // start by building a list of the entries that could spawn from the
    // provided biome tags.
    for current: WMH_BestiaryEntry in this.entries {
      if (current.canSpawn(
        biome_tags,
        day_hour,
        weather,
        global_biome,
        biome_tags_bypass
      )) {
        selected_entries.PushBack(current);
      }
    }

    var index: int = RandNoiseF(seed, selected_entries.Size()) as int;

    if (index < 0) {
      return NULL;
    }

    return selected_entries[index];
  }
}