function WMH_getBestiary(): WMH_Bestiary {
  return thePlayer.wmh.bestiary;
}

statemachine class WMH_Bestiary {
  private var master: WMH_Master;

  // the sub-components are public but only access them directly to submit
  // events from the master class.
  public var active_view: WMH_BestiaryActiveView;
  public var spawn_points: WMH_SpawnPointManager;
  public var biomes: WMH_BiomeManager;

  public var entries: array<WMH_BestiaryEntry>;
  public var entries_wildlife: array<WMH_BestiaryEntry>;

  protected var spawn_requests: array<WMH_SpawnRequest>;
  protected var spawn_instant_seed: WMH_DynInstantSeed;
  protected var wildlife_instant_seed: WMH_DynInstantSeed;

  public function init(master: WMH_Master): WMH_Bestiary {
    this.master = master;

    this.spawn_points = (new WMH_SpawnPointManager in this).init();
    this.active_view = (new WMH_BestiaryActiveView in this).init();
    this.biomes = (new WMH_BiomeManager in this).init();

    this.spawn_instant_seed = (new WMH_DynInstantSeed in this).init(30);
    this.wildlife_instant_seed = (new WMH_DynInstantSeed in this).init(120);

    this.initEntries();
    this.GotoState('Waiting');

    return this;
  }

  public function onSpawn(contract: WMH_ContractManager, hunt_seed: int) {
    this.maybeRefresh(contract);
    this.spawnNearbyAvailableSpawnPoints(contract, hunt_seed);
    this.spawnNearbyEventNpcSpawnPoints(hunt_seed);
    this.spawnNearbyGuardedTreasureSpawnPoints(hunt_seed);
  }

  /// Same logic as the regular onSpawn but delayed by a few seconds, to give
  /// enough time for regular spawn requests to be completed.
  public function onSpawnDelayed(
    contract: WMH_ContractManager,
    hunt_seed: int
  ) {
    // if the player is in a repulsive's area then skip spawning regular
    // encounters. It isn't as precise as checking if the spawn point itself is
    // in the repulsive but this approximation can save a lot of resources as
    // we skip everything until the player gets outside the repulsive's radius
    var can_spawn: bool = !WMH_getEncounterManager()
      .isInRepulsive(thePlayer.GetWorldPosition());

    if (can_spawn) {
      this.spawnNearbyAvailableSpawnPointFallbacks(contract, hunt_seed);
    }
    
  }

  public function onSpawnContractTarget(
    bentry: WMH_BestiaryEntry,
    location: WMH_BiomeSpawnPoint,
    affix_manager: WMH_AffixManager,
    encounter_tag: WMH_EncounterTag,
    seed: int
  ) {
    this.requestSpawn(
      (new WMH_SpawnRequest in this)
        .withBestiaryEntry(bentry)
        .withSpawnPoint(location)
        .withEncounterTag(encounter_tag)
        .withAffixes(affix_manager)
        .withSeed(seed)
    );
  }

  public function onSpawnClues(
    bentry: WMH_BestiaryEntry,
    location: WMH_BiomeSpawnPoint,
    clues_heading: float,
    seed: int
  ) {
    var request: WMH_SpawnRequestClues = new WMH_SpawnRequestClues in this;

    // do it in multiple lines to avoid downcasting it
    request.withBestiaryEntry(bentry.buildCluesEntry());
    request.withSpawnPoint(location);
    request.withHeading(clues_heading);
    request.withSeed(seed);

    this.requestSpawn(request);
  }

  public function onSpawnLure(
    origin: WMH_LureSpot,
    creature_bypass_tag: name,
    position: Vector,
    heading: float,
    runes_of_power_chance: float,
    hunt_seed: int
  ) {
    var bentry: WMH_BestiaryEntry = this.getEntryByBiomeTagByPass(creature_bypass_tag);

    if (!bentry) {
      return;
    }

    var request: WMH_SpawnRequestLure = new WMH_SpawnRequestLure in this;

    // do it in multiple lines to avoid downcasting it
    request.withBestiaryEntry(bentry);
    request.withSpawnPosition(position);
    request.withHeading(heading);
    request.withSeed(hunt_seed + origin.getSeed());
    // request.withAggressionTarget(thePlayer, 5);
    request.withRunesOfPowerChance(1, runes_of_power_chance);

    // todo: if lures can spawn contract targets
    // .withEncounterTag(encounter_tag)
    // .withAffixes(affix_manager)

    this.requestSpawn(request);
  }
  
  public function onSpawnEventHorde(
    origin: WMH_EventNpc_EventProtectHordeStateSpawning,
    bentry: WMH_BestiaryEntry,
    position: Vector,
    heading: float,
    event_seed: int
  ) {
    var request: WMH_SpawnRequestEventHorde;
    request = new WMH_SpawnRequestEventHorde in this;
    request.event_npc = origin;

    // do it in multiple lines to avoid downcasting it
    request.withBestiaryEntry(bentry);
    request.withSpawnPosition(position);
    request.withHeading(heading);
    request.withSeed(event_seed);

    this.requestSpawn(request);
  }

  public function onEventClearAreaStart(
    origin: WMH_EventNpc_EventClearPoi,
    point: WMH_BiomeSpawnPoint,
    event_seed: int,
  ) {
    this.trySpawnPoint(WMH_getContractManager(), point, event_seed, 1.0);
  }

  public function onSpawnGuardedTreasure(
    origin: WMH_GuardedTreasure,
    bentry: WMH_BestiaryEntry,
    position: Vector,
    treasure_seed: int
  ) {
    var request: WMH_SpawnRequestGuardedTreasure;
    request = new WMH_SpawnRequestGuardedTreasure in this;
    request.guarded_treasure = origin;

    // do it in multiple lines to avoid downcasting it
    request.withBestiaryEntry(bentry);
    request.withSpawnPosition(position);
    request.withSeed(treasure_seed);

    this.requestSpawn(request);
  }
  

  // The elite entries are the closest ones to the given WMH_Level
  public function getEliteEntries(level: WMH_Level): array<WMH_BestiaryEntry> {
    var output: array<WMH_BestiaryEntry>;
    var fallback: WMH_BestiaryEntry;

    while (output.Size() < 5) {
      for bentry: WMH_BestiaryEntry in this.entries {
        if (!fallback) {
          fallback = bentry;
        }

        if (bentry.canBeTarget(level)) {
          output.PushBack(bentry);
        }
        // the target could no be target, but it's stored as a possible fallback
        // if after looping through all targets we couldn't get 5 valid entries
        //
        // over time this should get us the strongest target below the WMH_Level
        else if (
          fallback.strength < level.value 
          && fallback.strength > bentry.strength
        ) {
          fallback = bentry;
        }
      }

      if (fallback) {
        // adjust the level to be around the fallback's strength, that should
        // open the door to new elite targets.
        level.value = fallback.strength;
      } else {
        break;
      }
    }

    return output;
  }

  // returns all of the spawn points (from the entire map) that would allow the
  // entry to spawn.
  public function getAvailableRegionSpawnPoints(
    bentry: WMH_BestiaryEntry
  ): array<WMH_BiomeSpawnPoint> {
    return this
      .getAvailableSpawnPointsForEntry(
        bentry,
        this.spawn_points.getRegionValidSpawnPoints()
      );
  }

  // returns the nearby spawn points that would allow the entry to spawn
  public function getAvailableNearbySpawnPoints(
    bentry: WMH_BestiaryEntry, optional ignore_respawn: bool
  ): array<WMH_BiomeSpawnPoint> {
    return this
      .getAvailableSpawnPointsForEntry(
        bentry,
        this.spawn_points.getValidSpawnPoints(ignore_respawn)
      );
  }

  public function getRandomUnfilteredEliteEntry(
    level: WMH_Level,
    seed: int
  ): WMH_BestiaryEntry {
    var elites: array<WMH_BestiaryEntry> = this.getEliteEntries(level);
    var index: int = RandNoiseF(seed, elites.Size() as int) as int;
    var bentry: WMH_BestiaryEntry = elites[index];

    return bentry;
  }

  public function getEntryByBiomeTagByPass(tag: name): WMH_BestiaryEntry {
    var null: WMH_BestiaryEntry;

    for bentry: WMH_BestiaryEntry in this.entries {
      if (bentry.biome_tag_bypass == tag) {
        return bentry;
      }
    }

    return null;
  }

  public function toPseudoRngBuffer(): WMH_PseudoRngBuffer {
    var buffer: WMH_PseudoRngBuffer = new WMH_PseudoRngBuffer in this;

    for bentry: WMH_BestiaryEntry in this.entries {
      buffer.registerEntry(bentry.strength);
    }

    return buffer;
  }

  private function spawnNearbyAvailableSpawnPoints(
    contract: WMH_ContractManager,
    hunt_seed: int
  ) {
    // if the player is in a repulsive's area then skip spawning regular
    // encounters. It isn't as precise as checking if the spawn point itself is
    // in the repulsive but this approximation can save a lot of resources as
    // we skip everything until the player gets outside the repulsive's radius
    var in_repulsive: bool = WMH_getEncounterManager()
      .isInRepulsive(thePlayer.GetWorldPosition());

    // 1.
    // first pass for monsters
    if (!in_repulsive) {
      var spawn_points: array<WMH_BiomeSpawnPoint> = this.spawn_points
        .getValidSpawnPoints();

      var instant_seed: int = this.spawn_instant_seed
        .dynSeedPlayerMovements(hunt_seed, 3.0);

      var monster_spawn_chance: float = 1.0;
      var wildlife_spawn_chance: float = 0.0;
      var spawned_count: int = 0;

      for point: WMH_BiomeSpawnPoint in spawn_points {
        spawned_count += this.trySpawnPoint(
          contract,
          point,
          instant_seed,
          monster_spawn_chance,
          wildlife_spawn_chance
        ) as int;
      }

      WMHINFO("spawnNearbyAvailableSpawnPoints(), monsters spawned_count = " + spawned_count);
    }
    else {
      WMHINFO("spawnNearbyAvailableSpawnPoints(), monsters, cancelled, in repulsive");
    }

    // 2.
    // second pass for wildlife
    var spawn_points: array<WMH_BiomeSpawnPoint> = this.spawn_points
      .getValidSpawnPoints(true);

    var instant_seed: int = this.wildlife_instant_seed
      .dynSeedPlayerMovements(hunt_seed, 10.0);

    // repulsive prevents monsters from spawning, instead the amount of wildlife
    // increases.
    var wildlife_spawn_chance: float = 0.3 + (in_repulsive as float) * 0.2;
    var monster_spawn_chance: float = 0.0;
    var spawned_count: int = 0;

    for point: WMH_BiomeSpawnPoint in spawn_points {
      spawned_count += this.trySpawnPoint(
        contract,
        point,
        instant_seed,
        monster_spawn_chance,
        wildlife_spawn_chance
      ) as int;
    }

    WMHINFO("spawnNearbyAvailableSpawnPoints(), wildlife spawned_count = " + spawned_count);
  }

  private function spawnNearbyAvailableSpawnPointFallbacks(
    contract: WMH_ContractManager,
    hunt_seed: int
  ) {
    var spawn_points: array<WMH_BiomeSpawnPointFallback>;
    this.spawn_points.getNearbyValidSpawnPointFallbacks(spawn_points);

    var instant_seed: int = this.spawn_instant_seed
      .dynSeedPlayerMovements(hunt_seed, 3.0);

    var spawned_count: int = 0;

    for point: WMH_BiomeSpawnPoint in spawn_points {
      var nearby_respawning_points: int = this.spawn_points
        .countNearbyWaitingForRespawnSpawnPoints(point.GetWorldPosition(), 40);

      var altered_chance: float = 0.1 * nearby_respawning_points;
      var point_seed: int = point.getPointSeed(instant_seed);

      // for every occupied point nearby, the chances of this point being used
      var can_spawn_monsters: bool = altered_chance > 0.5
        && point.canSpawnMonstersInHunt(point_seed, altered_chance);

      if (!can_spawn_monsters) {
        continue;
      }

      var can_spawn_wildlife: bool = RandNoiseF(point_seed + 1, 1.0)
        <= 0.05 + (point.prefer_wildlife as float) * 0.1;

      if (can_spawn_wildlife) {
        var bentry: WMH_BestiaryEntry = this.active_view.getRandomEntryWildlife(
          this.biomes,
          point.biome_tags,
          point.biome_tags_bypass,
          point_seed
        );
      }
      else {
        var options: WMH_getRandomEntryOptions;
        options.exclude_strong_target = !point.allow_strong_targets;
        options.exclude_weak_target = !point.allow_weak_targets;

        var bentry: WMH_BestiaryEntry = this.active_view.getRandomEntry(
          contract,
          this.biomes,
          point.biome_tags,
          point.biome_tags_bypass,
          point_seed,
          options
        );
      }

      if (bentry) {
        var request: WMH_SpawnRequest = (new WMH_SpawnRequest in this)
          .init()
          .withSpawnPoint(point)
          .withBestiaryEntry(bentry)
          .withSeed(point_seed);

        this.requestSpawn(request);
        point.consume();
        spawned_count += 1;
      }
      else {
        WMHINFO("WMH_Bestiary::spawnRandomEntry() - no valid bestiary entry");
      }
    }

    WMHINFO("spawnNearbyAvailableSpawnPoints(), spawned_count = " + spawned_count);
  }

  private function trySpawnPoint(
    contract: WMH_ContractManager,
    point: WMH_BiomeSpawnPoint,
    instant_seed: int,
    monster_spawn_chance: float,
    optional wildlife_spawn_chance: float
  ): bool {
    var point_seed: int = point.getPointSeed(instant_seed);
    var can_spawn_monsters: bool = false;
    var can_spawn_wildlife: bool = false;

    if (monster_spawn_chance > 0.0) {
      var nearby_occupied_count: int = this.spawn_points
        .countNearbyOccupiedSpawnPoints(point.GetWorldPosition(), 55);

      // for every occupied point nearby, the chances of this point being used
      // decreases
      var altered_chance: float = monster_spawn_chance / (nearby_occupied_count + 1);

      can_spawn_monsters = point
        .canSpawnMonstersInHunt(point_seed, altered_chance);
    }

    if (wildlife_spawn_chance > 0.0) {
      can_spawn_wildlife = (
        RandNoiseF(point_seed + 1, 1.0)
        <= wildlife_spawn_chance * ((point.prefer_wildlife as float) + 1.0)
      ) && point.canSpawnWildlife();

      // WMHINFO("trySpawnPoint(), can_spawn_wildlife = " + can_spawn_wildlife);
    }

    // WMHINFO(
    //   "trySpawnPoint(),"
    //   + " monster, chance = " + monster_spawn_chance + ", can_spawn = " + can_spawn_monsters
    //   + " wildlife, chance = " + wildlife_spawn_chance + ", can_spawn = " + can_spawn_wildlife
    // );

    var bentries: array<WMH_BestiaryEntry>;
    if (can_spawn_wildlife) {
      var bentry: WMH_BestiaryEntry = this.active_view.getRandomEntryWildlife(
        this.biomes,
        point.biome_tags,
        point.biome_tags_bypass,
        point_seed
      );

      if (bentry) {
        bentries.PushBack(bentry);
      }
      else {
        WMHINFO("WMH_Bestiary::spawnRandomEntry() - no valid wildlife entry");
      }
    }
    
    if (can_spawn_monsters) {
      var options: WMH_getRandomEntryOptions;
      options.exclude_strong_target = !point.allow_strong_targets;
      options.exclude_weak_target = !point.allow_weak_targets;

      var bentry: WMH_BestiaryEntry = this.active_view.getRandomEntry(
        contract,
        this.biomes,
        point.biome_tags,
        point.biome_tags_bypass,
        point_seed,
        options
      );

      if (bentry) {
        bentries.PushBack(bentry);
      }
      else {
        WMHINFO("WMH_Bestiary::spawnRandomEntry() - no valid monster entry");
      }
    }

    for bentry: WMH_BestiaryEntry in bentries {
      var request: WMH_SpawnRequest = (new WMH_SpawnRequest in this)
        .init()
        .withSpawnPoint(point)
        .withBestiaryEntry(bentry)
        .withSeed(point_seed);

      this.requestSpawn(request);
    }

    if (bentries.Size() > 0) {
      if (can_spawn_monsters) {
        point.consume();
      }

      if (can_spawn_wildlife) {
        point.consumeForWildlife();
      }

      return true;
    }

    return false;
  }

  private function spawnNearbyEventNpcSpawnPoints(hunt_seed: int) {
    var points: array<WMH_EventNpcSpawnPoint>;

    this.spawn_points.getNearbyEventNpcSpawnPoints(points);

    for point: WMH_EventNpcSpawnPoint in points {
      point.trySpawnNpc(hunt_seed);
    }
  }

  private function spawnNearbyGuardedTreasureSpawnPoints(hunt_seed: int) {
    var points: array<WMH_GuardTreasureSpawnPoint>;

    this.spawn_points.getNearbyGuardedTreasureSpawnPoints(points);

    for point: WMH_GuardTreasureSpawnPoint in points {
      point.trySpawnGuardedTreasure(hunt_seed);
    }
  }

  private function requestSpawn(request: WMH_SpawnRequest) {
    this.spawn_requests.PushBack(request);

    if (this.GetCurrentStateName() == 'Waiting') {
      this.GotoState('Spawning');
    }
  }

  private function maybeRefresh(contract: WMH_ContractManager) {
    this.spawn_points.maybeRefresh();
    this.active_view.maybeRefresh(
      this.entries,
      this.entries_wildlife,
      this.biomes,
      contract
    );
  }

  private function initEntries() {
    var index: int = 0;

    index = this.initEntries_0(index);
    index = this.initEntries_1(index);
  }

  private function initEntries_0(index: int): int {
    #define function registerBestiaryEntry(entry_name) {
      var bentry_entry_name: WMH_BestiaryEntryentry_name =
        (new WMH_BestiaryEntryentry_name in this);
      bentry_entry_name.init(WMH_BestiaryId(index));
      this.entries.PushBack(bentry_entry_name);
      index += 1;
    };
    registerBestiaryEntry!(Alghoul)
    registerBestiaryEntry!(Arachas)
    registerBestiaryEntry!(Barghest)
    registerBestiaryEntry!(Basilisk)
    registerBestiaryEntry!(Bear)
    registerBestiaryEntry!(Berserker)
    registerBestiaryEntry!(Boar)
    registerBestiaryEntry!(Bruxa)
    registerBestiaryEntry!(Bruxacity)
    registerBestiaryEntry!(Centipede)
    registerBestiaryEntry!(Chort)
    registerBestiaryEntry!(Cockatrice)
    registerBestiaryEntry!(Cyclop)
    registerBestiaryEntry!(Detlaff)
    registerBestiaryEntry!(Dracolizard)
    registerBestiaryEntry!(Drowner)
    registerBestiaryEntry!(Echinops)
    registerBestiaryEntry!(Ekimmara)
    registerBestiaryEntry!(Elemental)
    registerBestiaryEntry!(Endrega)
    registerBestiaryEntry!(Fiend)
    registerBestiaryEntry!(Fleder)
    registerBestiaryEntry!(Fogling)
    registerBestiaryEntry!(Forktail)
    registerBestiaryEntry!(Gargoyle)
    registerBestiaryEntry!(Garkain)
    registerBestiaryEntry!(Ghoul)
    registerBestiaryEntry!(Giant)
    registerBestiaryEntry!(Golem)
    registerBestiaryEntry!(GolemIce)
    registerBestiaryEntry!(Gravier)
    registerBestiaryEntry!(Griffin)
    registerBestiaryEntry!(Hag)
    registerBestiaryEntry!(Harpy)
    registerBestiaryEntry!(Human)
    registerBestiaryEntry!(Katakan)
    registerBestiaryEntry!(Kikimore)
    registerBestiaryEntry!(Leshen)
    registerBestiaryEntry!(LeshenGrotto)
    registerBestiaryEntry!(Nekker)
    registerBestiaryEntry!(Nightwraith)
    registerBestiaryEntry!(Noonwraith)
    /*
    registerBestiaryEntry!(Panther)
    */
    registerBestiaryEntry!(Rotfiend)
    registerBestiaryEntry!(Sharley)
    registerBestiaryEntry!(Siren)
    registerBestiaryEntry!(Skelbear)
    registerBestiaryEntry!(Skeleton)
    registerBestiaryEntry!(Skeltroll)
    registerBestiaryEntry!(Skelwolf)
    registerBestiaryEntry!(Spider)
    registerBestiaryEntry!(Troll)
    registerBestiaryEntry!(Werewolf)
    registerBestiaryEntry!(Wight)
    registerBestiaryEntry!(Wildhunt)
    registerBestiaryEntry!(Wolf)
    registerBestiaryEntry!(Wraith)
    registerBestiaryEntry!(Wyvern)

    return index;
  }

  private function initEntries_1(index: int): int {
    #define function registerBestiaryEntryWildlife(entry_name) {
      var bentry_entry_name: WMH_BestiaryEntryentry_name =
        (new WMH_BestiaryEntryentry_name in this);
      bentry_entry_name.init(WMH_BestiaryId(index));
      this.entries_wildlife.PushBack(bentry_entry_name);
      index += 1;
    };

    // wildlife
    registerBestiaryEntryWildlife!(Bat)
    registerBestiaryEntryWildlife!(Cat)
    registerBestiaryEntryWildlife!(Chicken)
    registerBestiaryEntryWildlife!(Cow)
    registerBestiaryEntryWildlife!(Deer)
    registerBestiaryEntryWildlife!(Deer_roe)
    registerBestiaryEntryWildlife!(Dog)
    registerBestiaryEntryWildlife!(Goat)
    registerBestiaryEntryWildlife!(Guard_dog)
    registerBestiaryEntryWildlife!(Hare)
    registerBestiaryEntryWildlife!(Mountain_goat)
    registerBestiaryEntryWildlife!(Mouse)
    registerBestiaryEntryWildlife!(Pig)
    registerBestiaryEntryWildlife!(Pigeon)
    registerBestiaryEntryWildlife!(Ram)
    registerBestiaryEntryWildlife!(Rat)
    registerBestiaryEntryWildlife!(Rooster)
    registerBestiaryEntryWildlife!(Sheep)
    registerBestiaryEntryWildlife!(Snow_deer)
    registerBestiaryEntryWildlife!(Snow_rabbit)
    /*
    registerBestiaryEntryWildlife!(Crab)
    registerBestiaryEntryWildlife!(Crow)
    registerBestiaryEntryWildlife!(Fish_kingfish)
    registerBestiaryEntryWildlife!(Fish_mackerel)
    registerBestiaryEntryWildlife!(Fish_roach)
    registerBestiaryEntryWildlife!(Fish_tuna)
    registerBestiaryEntryWildlife!(Goose)
    registerBestiaryEntryWildlife!(Goose_leader)
    registerBestiaryEntryWildlife!(Owl)
    registerBestiaryEntryWildlife!(Seagull)
    registerBestiaryEntryWildlife!(Sparrow)
    registerBestiaryEntryWildlife!(Swallow)
    registerBestiaryEntryWildlife!(Toad)
    */

    return index;
  }

  
  private function getAvailableSpawnPointsForEntry(
    bentry: WMH_BestiaryEntry,
    spawn_points: array<WMH_BiomeSpawnPoint>
  ): array<WMH_BiomeSpawnPoint> {
    var output: array<WMH_BiomeSpawnPoint>;

    var day_hour: int = this.biomes.getDayHour();
    var weather: WMH_WeatherType = this.biomes.getWeatherType();
    var global_biome: WMH_BiomeTags = this.biomes.getGlobalBiome();

    var point: WMH_BiomeSpawnPoint;
    for point: WMH_BiomeSpawnPoint in spawn_points {
      // if the point has empty tags then everything can spawn there
      var can_spawn: bool = WMH_biomeTagsEmpty(point.biome_tags);

      can_spawn = can_spawn || bentry.canSpawn(
        WMH_mergeBiomeTags(point.biome_tags, global_biome),
        day_hour,
        weather,
        point.biome_tags_bypass,
      );

      if (can_spawn) {
        output.PushBack(point);
      }
    }

    return output;
  }
}

state Waiting in WMH_Bestiary {}

state Spawning in WMH_Bestiary {
  event OnEnterState(previous_state_name: name) {
		super.OnEnterState(previous_state_name);
    this.Spawning_main();
	}

  entry function Spawning_main() {
    // WMHINFO("WMH_BestiaryStateSpawning::Spawning_main()");

    while (parent.spawn_requests.Size() > 0) {
      var request: WMH_SpawnRequest = parent.spawn_requests.PopBack();

      if (request) {
        this.performSpawnRequest(request);
        SleepOneFrame();
      }
    }

    parent.GotoState('Waiting');
  }

  latent function performSpawnRequest(request: WMH_SpawnRequest) {
    var bentry: WMH_BestiaryEntry;
    var entities: array<CEntity>;

    var position: Vector = request.GetWorldPosition();
    var contract_manager: WMH_ContractManager = WMH_getContractManager();
    var encounter_manager: WMH_EncounterManager = WMH_getEncounterManager();
    for bentry: WMH_BestiaryEntry in request.entries {
      // repulsives prevent regular monsters from spawning, contract targets
      // however are unaffected by them.
      if (
        encounter_manager.isInRepulsive(position)
        && !contract_manager.isTargetted(bentry.id)
      ) {
        continue;
      }

      this.spawnTemplates(
        bentry,
        request.GetWorldPosition(),
        request.heading,
        request.affix_manager,
        request.seed,
        /*out*/ entities
      );
    }

    if (request.aggression_target) {
      SUH_makeEntitiesTargetPlayer(entities);
    }

    // add runes of power to entities
    var i: int = 0;
    for (i = 0; i < request.runes_of_power_count; i += 1) {
      var seed: int = request.seed + i;
      var can_add: bool = RandNoiseF(seed + 10, 1.0)
                        < request.runes_of_power_chance;

      if (!can_add) {
        continue;
      }

      var index: int = RandNoiseF(seed + 100, entities.Size()) as int;
      var entity: CEntity = entities[index];
      var actor: CActor = entity as CActor;

      if (actor) {
        thePlayer.wmh.loot.giveRandomRuneOfPower(actor.GetInventory(), seed);
      }
      
    }

    request.onRequestFinished(entities);
  }

  latent function spawnTemplates(
    bentry: WMH_BestiaryEntry,
    position: Vector,
    heading: float,
    affix_manager: WMH_AffixManager,
    seed: int,
    out entities: array<CEntity>
  ) {
    var templates: array<WMH_SpawnTemplateAndCount> = bentry.buildSpawnList(seed);

    var group_positions: array<Vector> = WMH_getGroupPositions(
      position,
      templates.Size(),
      /*density*/ 0.01
    );

    var tags_array: array<name>;
    tags_array.PushBack('WildMonsterHuntEntity');

    var i: int;
    for (i = 0; i < templates.Size(); i += 1) {
      var template_path: string = templates[i].template;

      WMHINFO("template = " + template_path);

      // todo: it will load the same templates multiple times
      var entity_template: CEntityTemplate = LoadResourceAsync(
        template_path, true
      ) as CEntityTemplate;
      SleepOneFrame();

      // WMH_FixZAxis(/*out*/ group_positions[i]);

      var current_rotation: EulerAngles;

      if (heading != 0) {
        current_rotation = VecToRotation(
          // with a +5/-5 variation
          VecFromHeading(heading + RandRangeF(10.0, 0.0) - 5.0)
        );
      }
      else {
        current_rotation = VecToRotation(VecRingRand(1, 2));
      }

      var persistance: EPersistanceMode = PM_DontPersist;

      var i_1: int = 0;
      for (i_1 = 0; i_1 < templates[i].count; i_1 += 1) {
        var created_entity: CEntity = theGame.CreateEntity(
          entity_template,
          group_positions[i],
          current_rotation,,,,
          persistance,
          tags_array
        );

        bentry.onEntitySpawned(created_entity);

        var npc: CNewNPC = created_entity as CNewNPC;
        if (npc) {
          if (affix_manager) {
            affix_manager.applyToNpc(npc);
          }
        }

        entities.PushBack(created_entity);
      }
    }
  }
}

struct WMH_SpawnTemplateAndCount {
  var template: string;
  var count: int;
}


function WMH_getGroupPositions(initial_position: Vector, count: int, density: float): array<Vector> {
  var s, r, x, y: float;
  var pos_fin: Vector;
  var output_positions: array<Vector>;
  
  var i: int;
  var sign: int;

  //const values used in the loop
  pos_fin.Z = initial_position.Z;
  s = count / density; // maintain a constant density of `density` unit per m2
  r = SqrtF(s/Pi());

  for (i = 0; i < count; i += 1) {
    x = RandF() * r;        // add random value within range to X
    y = RandF() * (r - x);  // add random value to Y so that the point is within the disk

    if(RandRange(2)) {       // randomly select the sign for misplacement
      sign = 1;
    }
    else {
      sign = -1;
    }
      
    pos_fin.X = initial_position.X + sign * x;  //final X pos
    
    if(RandRange(2)) {        // randomly select the sign for misplacement
      sign = 1;
    }
    else {
      sign = -1;
    }
      
    pos_fin.Y = initial_position.Y + sign * y;  //final Y pos

    // return false means it could not find ground position
    // in this case, take the default position
    // if return true, then pos_fin is updated with the correct position
    // if (!WMH_getGroundPosition(pos_fin)) {
    //   pos_fin = initial_position;
    // }

    output_positions.PushBack(pos_fin);
  }

  return output_positions;
}

function WMH_getGroundPosition(out input_position: Vector, optional personal_space: float, optional radius: float): bool {
  var found_viable_position: bool;
  var collision_normal: Vector;
  var max_height_check: float;
  var output_position: Vector;
  var point_z: float;
  var attempts: int;

  attempts = 10;
  output_position = input_position;
  personal_space = MaxF(personal_space, 1.0);
  max_height_check = 30.0;

  if (radius == 0) {
    radius = 10.0;
  }

  do {
    attempts -= 1;

    // first search for ground based on navigation data.
    theGame
    .GetWorld()
    .NavigationComputeZ(
      output_position,
      output_position.Z - max_height_check,
      output_position.Z + max_height_check,
      point_z
    );

    output_position.Z = point_z;

    if (!theGame.GetWorld().NavigationFindSafeSpot(output_position, personal_space, radius, output_position)) {
      continue;
    }

    // then do a static trace to find the position on ground
    // ... okay i'm not sure anymore, is the static trace needed?
    // theGame
    // .GetWorld()
    // .StaticTrace(
    //   output_position + Vector(0,0,1.5),// + 5,// Vector(0,0,5),
    //   output_position - Vector(0,0,1.5),// - 5,//Vector(0,0,5),
    //   output_position,
    //   collision_normal
    // );

    // finally, return if the position is above water level
    if (output_position.Z < theGame.GetWorld().GetWaterLevel(output_position, true)) {
      continue;
    }

    found_viable_position = true;
    break;
  } while (attempts > 0);


  if (found_viable_position) {
    input_position = output_position;

    return true;
  }

  return false;
}

function WMH_FixZAxis(out pos : Vector) {
  var world : CWorld;
  var z : float;

  world = theGame.GetWorld();

  if (world.NavigationComputeZ(pos, pos.Z - 128, pos.Z + 128, z)) {
    pos.Z = z;
  }
  else if (world.PhysicsCorrectZ(pos, z)) {
    pos.Z = z;
  }
}