

statemachine class WMH_Bestiary {
  private var master: WMH_Master;

  private var active_view: WMH_BestiaryActiveView;
  private var spawn_points: WMH_SpawnPointManager;
  private var biomes: WMH_BiomeManager;

  public var entries: array<WMH_BestiaryEntry>;
  public var wildlife: array<WMH_BestiaryEntry>;

  protected var spawn_requests: array<WMH_SpawnRequest>;

  public function init(master: WMH_Master): WMH_Bestiary {
    this.master = master;

    this.spawn_points = (new WMH_SpawnPointManager in this).init();
    this.active_view = (new WMH_BestiaryActiveView in this).init();
    this.biomes = (new WMH_BiomeManager in this).init();

    this.initEntries();
    this.GotoState('Waiting');

    return this;
  }

  public function onHuntStarted() {
    this.spawn_points.onHuntStarted();
  }

  public function onSpawn(contract: WMH_ContractManager, hunt_seed: int) {
    this.maybeRefresh(contract);
    this.spawnRandomEntry(contract, hunt_seed);
  }

  public function onSpawnContractTarget(
    bentry: WMH_BestiaryEntry,
    location: WMH_BiomeSpawnPoint,
    seed: int
  ) {
    this.requestSpawn(
      (new WMH_SpawnRequest in this)
        .withBestiaryEntry(bentry)
        .withPosition(location.GetWorldPosition())
        .withSeed(seed)
    );
  }

  public function getEliteEntries(level: WMH_Level): array<WMH_BestiaryEntry> {
    var output: array<WMH_BestiaryEntry>;

    var bentry: WMH_BestiaryEntry;
    for bentry: WMH_BestiaryEntry in this.entries {
      if (bentry.isElite(level)) {
        output.PushBack(bentry);
      }
    }

    return output;
  }

  // returns all of the spawn points (from the entire map) that would allow the
  // entry to spawn.
  public function getAvailableRegionSpawnPoints(
    bentry: WMH_BestiaryEntry
  ): array<WMH_BiomeSpawnPoint> {
    return this
      .getAvailableSpawnPointsForEntry(
        bentry,
        this.spawn_points.getRegionValidSpawnPoints()
      );
  }

  private function spawnRandomEntry(
    contract: WMH_ContractManager,
    seed: int
  ) {
    var spawn_point: WMH_BiomeSpawnPoint = this.spawn_points
      .getRandomNearbyValidSpawnPoint(seed);

    if (spawn_point) {
      var position: Vector = spawn_point.GetWorldPosition();

      var bentry: WMH_BestiaryEntry = this.active_view.getRandomEntry(
        contract,
        this.biomes,
        spawn_point.biome_tags,
        spawn_point.biome_tags_bypass,
        seed + (position.X as int) - (position.Y as int)
      );

      if (bentry) {
        var request: WMH_SpawnRequest = (new WMH_SpawnRequest in this)
          .init()
          .withPosition(spawn_point.GetWorldPosition())
          .withBestiaryEntry(bentry)
          .withSeed(seed);

        this.requestSpawn(request);
      }
      else {
        WMHINFO("WMH_Bestiary::spawnRandomEntry() - no valid bestiary entry");
      }
    } else {
      WMHINFO("WMH_Bestiary::spawnRandomEntry() - no valid spawn point");
    }
  }

  private function requestSpawn(request: WMH_SpawnRequest) {
    this.spawn_requests.PushBack(request);

    if (this.GetCurrentStateName() == 'Waiting') {
      this.GotoState('Spawning');
    }
  }

  private function maybeRefresh(contract: WMH_ContractManager) {
    this.spawn_points.maybeRefresh();
    this.active_view.maybeRefresh(this.entries, this.biomes, contract);
  }

  private function initEntries() {
    var index: int = 0;

    #define function registerBestiaryEntry(entry_name) {
      var bentry_entry_name: WMH_BestiaryEntryentry_name =
        (new WMH_BestiaryEntryentry_name in this);
      bentry_entry_name.init(WMH_BestiaryId(index));
      this.entries.PushBack(bentry_entry_name);
      index += 1;
    };

    #define function registerBestiaryEntryWildlife(entry_name) {
      var bentry_entry_name: WMH_BestiaryEntryentry_name =
        (new WMH_BestiaryEntryentry_name in this);
      bentry_entry_name.init(WMH_BestiaryId(index));
      this.wildlife.PushBack(bentry_entry_name);
      index += 1;
    };

    registerBestiaryEntry!(Alghoul)
    registerBestiaryEntry!(Arachas)
    registerBestiaryEntry!(Barghest)
    registerBestiaryEntry!(Basilisk)
    registerBestiaryEntry!(Bear)
    registerBestiaryEntry!(Berserker)
    registerBestiaryEntry!(Boar)
    registerBestiaryEntry!(Bruxa)
    registerBestiaryEntry!(Bruxacity)
    registerBestiaryEntry!(Centipede)
    registerBestiaryEntry!(Chort)
    registerBestiaryEntry!(Cockatrice)
    registerBestiaryEntry!(Cyclop)
    registerBestiaryEntry!(Detlaff)
    registerBestiaryEntry!(Dracolizard)
    registerBestiaryEntry!(Drowner)
    registerBestiaryEntry!(Echinops)
    registerBestiaryEntry!(Ekimmara)
    registerBestiaryEntry!(Elemental)
    registerBestiaryEntry!(Endrega)
    registerBestiaryEntry!(Fiend)
    registerBestiaryEntry!(Fleder)
    registerBestiaryEntry!(Fogling)
    registerBestiaryEntry!(Forktail)
    registerBestiaryEntry!(Gargoyle)
    registerBestiaryEntry!(Garkain)
    registerBestiaryEntry!(Ghoul)
    registerBestiaryEntry!(Giant)
    registerBestiaryEntry!(Golem)
    registerBestiaryEntry!(Gravier)
    registerBestiaryEntry!(Gryphon)
    registerBestiaryEntry!(Hag)
    registerBestiaryEntry!(Harpy)
    registerBestiaryEntry!(Human)
    registerBestiaryEntry!(Katakan)
    registerBestiaryEntry!(Kikimore)
    registerBestiaryEntry!(Leshen)
    registerBestiaryEntry!(LeshenGrotto)
    registerBestiaryEntry!(Nekker)
    registerBestiaryEntry!(Nightwraith)
    registerBestiaryEntry!(Noonwraith)
    registerBestiaryEntry!(Panther)
    registerBestiaryEntry!(Rotfiend)
    registerBestiaryEntry!(Sharley)
    registerBestiaryEntry!(Siren)
    registerBestiaryEntry!(Skelbear)
    registerBestiaryEntry!(Skeleton)
    registerBestiaryEntry!(Skeltroll)
    registerBestiaryEntry!(Skelwolf)
    registerBestiaryEntry!(Spider)
    registerBestiaryEntry!(Troll)
    registerBestiaryEntry!(Werewolf)
    registerBestiaryEntry!(Wight)
    registerBestiaryEntry!(Wildhunt)
    registerBestiaryEntry!(Wolf)
    registerBestiaryEntry!(Wraith)
    registerBestiaryEntry!(Wyvern)

    // wildlife
    registerBestiaryEntryWildlife!(Bat)
    registerBestiaryEntryWildlife!(Cat)
    registerBestiaryEntryWildlife!(Chicken)
    registerBestiaryEntryWildlife!(Cow)
    registerBestiaryEntryWildlife!(Crab)
    registerBestiaryEntryWildlife!(Crow)
    registerBestiaryEntryWildlife!(Deer)
    registerBestiaryEntryWildlife!(Deer_roe)
    registerBestiaryEntryWildlife!(Dog)
    registerBestiaryEntryWildlife!(Fish_kingfish)
    registerBestiaryEntryWildlife!(Fish_mackerel)
    registerBestiaryEntryWildlife!(Fish_roach)
    registerBestiaryEntryWildlife!(Fish_tuna)
    registerBestiaryEntryWildlife!(Goat)
    registerBestiaryEntryWildlife!(Goose)
    registerBestiaryEntryWildlife!(Goose_leader)
    registerBestiaryEntryWildlife!(Guard_dog)
    registerBestiaryEntryWildlife!(Hare)
    registerBestiaryEntryWildlife!(Mountain_goat)
    registerBestiaryEntryWildlife!(Mouse)
    registerBestiaryEntryWildlife!(Owl)
    registerBestiaryEntryWildlife!(Pig)
    registerBestiaryEntryWildlife!(Pigeon)
    registerBestiaryEntryWildlife!(Ram)
    registerBestiaryEntryWildlife!(Rat)
    registerBestiaryEntryWildlife!(Rooster)
    registerBestiaryEntryWildlife!(Seagull)
    registerBestiaryEntryWildlife!(Sheep)
    registerBestiaryEntryWildlife!(Snow_deer)
    registerBestiaryEntryWildlife!(Snow_rabbit)
    registerBestiaryEntryWildlife!(Sparrow)
    registerBestiaryEntryWildlife!(Swallow)
    registerBestiaryEntryWildlife!(Toad)
  }

  
  private function getAvailableSpawnPointsForEntry(
    bentry: WMH_BestiaryEntry,
    spawn_points: array<WMH_BiomeSpawnPoint>
  ): array<WMH_BiomeSpawnPoint> {
    var output: array<WMH_BiomeSpawnPoint>;

    var day_hour: int = this.biomes.getDayHour();
    var weather: WMH_WeatherType = this.biomes.getWeatherType();
    var global_biome: WMH_BiomeTags = this.biomes.getGlobalBiome();

    var point: WMH_BiomeSpawnPoint;
    for point: WMH_BiomeSpawnPoint in spawn_points {
      // if the point has empty tags then everything can spawn there
      var can_spawn: bool = WMH_biomeTagsEmpty(point.biome_tags);

      can_spawn = can_spawn || bentry.canSpawn(
        WMH_mergeBiomeTags(point.biome_tags, global_biome),
        day_hour,
        weather,
        point.biome_tags_bypass,
      );

      if (can_spawn) {
        output.PushBack(point);
      }
    }

    return output;
  }
}

state Waiting in WMH_Bestiary {
  event OnEnterState(previous_state_name: name) {
		super.OnEnterState(previous_state_name);
    this.Waiting_main();
	}

  entry function Waiting_main() {
    WMHINFO("WMH_BestiaryStateWaiting::Waiting_main()");
  }
}

state Spawning in WMH_Bestiary {
  event OnEnterState(previous_state_name: name) {
		super.OnEnterState(previous_state_name);
    this.Spawning_main();
	}

  entry function Spawning_main() {
    WMHINFO("WMH_BestiaryStateSpawning::Spawning_main()");


    while (parent.spawn_requests.Size() > 0) {
      var request: WMH_SpawnRequest = parent.spawn_requests.PopBack();

      if (request) {
        this.performSpawnRequest(request);
      }
    }

    parent.GotoState('Waiting');
  }

  latent function performSpawnRequest(request: WMH_SpawnRequest) {
    var bentry: WMH_BestiaryEntry;
    var entities: array<CEntity>;

    for bentry: WMH_BestiaryEntry in request.entries {
      this.spawnTemplates(
        bentry,
        request.position,
        request.seed,
        /*out*/ entities
      );
    }

    request.onRequestFinished(entities);
  }

  latent function spawnTemplates(
    bentry: WMH_BestiaryEntry,
    position: Vector,
    seed: int,
    out entities: array<CEntity>
  ) {
    var templates: array<string> = bentry.buildSpawnList(seed);

    var group_positions: array<Vector> = WMH_getGroupPositions(
      position,
      templates.Size(),
      /*density*/ 0.01
    );

    var tags_array: array<name>;
    tags_array.PushBack('WildMonsterHuntEntity');

    var i: int;
    for (i = 0; i < templates.Size(); i += 1) {
      var template_path: string = templates[i];

      // todo: it will load the same templates multiple times
      var entity_template: CEntityTemplate = LoadResourceAsync(
        template_path, true
      ) as CEntityTemplate;

      WMH_FixZAxis(/*out*/ group_positions[i]);

      var current_rotation: EulerAngles = VecToRotation(VecRingRand(1, 2));
      var persistance: EPersistanceMode = PM_DontPersist;

      var created_entity: CEntity = theGame.CreateEntity(
        entity_template,
        group_positions[i],
        current_rotation,,,,
        persistance,
        tags_array
      );

      bentry.onEntitySpawned(created_entity);
      entities.PushBack(created_entity);
    }
  }
}


function WMH_getGroupPositions(initial_position: Vector, count: int, density: float): array<Vector> {
  var s, r, x, y: float;
  var pos_fin: Vector;
  var output_positions: array<Vector>;
  
  var i: int;
  var sign: int;

  //const values used in the loop
  pos_fin.Z = initial_position.Z;
  s = count / density; // maintain a constant density of `density` unit per m2
  r = SqrtF(s/Pi());

  for (i = 0; i < count; i += 1) {
    x = RandF() * r;        // add random value within range to X
    y = RandF() * (r - x);  // add random value to Y so that the point is within the disk

    if(RandRange(2)) {       // randomly select the sign for misplacement
      sign = 1;
    }
    else {
      sign = -1;
    }
      
    pos_fin.X = initial_position.X + sign * x;  //final X pos
    
    if(RandRange(2)) {        // randomly select the sign for misplacement
      sign = 1;
    }
    else {
      sign = -1;
    }
      
    pos_fin.Y = initial_position.Y + sign * y;  //final Y pos

    // return false means it could not find ground position
    // in this case, take the default position
    // if return true, then pos_fin is updated with the correct position
    if (!WMH_getGroundPosition(pos_fin)) {
      pos_fin = initial_position;
    }

    output_positions.PushBack(pos_fin);
  }

  return output_positions;
}

function WMH_getGroundPosition(out input_position: Vector, optional personal_space: float, optional radius: float): bool {
  var found_viable_position: bool;
  var collision_normal: Vector;
  var max_height_check: float;
  var output_position: Vector;
  var point_z: float;
  var attempts: int;

  attempts = 10;
  output_position = input_position;
  personal_space = MaxF(personal_space, 1.0);
  max_height_check = 30.0;

  if (radius == 0) {
    radius = 10.0;
  }

  do {
    attempts -= 1;

    // first search for ground based on navigation data.
    theGame
    .GetWorld()
    .NavigationComputeZ(
      output_position,
      output_position.Z - max_height_check,
      output_position.Z + max_height_check,
      point_z
    );

    output_position.Z = point_z;

    if (!theGame.GetWorld().NavigationFindSafeSpot(output_position, personal_space, radius, output_position)) {
      continue;
    }

    // then do a static trace to find the position on ground
    // ... okay i'm not sure anymore, is the static trace needed?
    // theGame
    // .GetWorld()
    // .StaticTrace(
    //   output_position + Vector(0,0,1.5),// + 5,// Vector(0,0,5),
    //   output_position - Vector(0,0,1.5),// - 5,//Vector(0,0,5),
    //   output_position,
    //   collision_normal
    // );

    // finally, return if the position is above water level
    if (output_position.Z < theGame.GetWorld().GetWaterLevel(output_position, true)) {
      continue;
    }

    found_viable_position = true;
    break;
  } while (attempts > 0);


  if (found_viable_position) {
    input_position = output_position;

    return true;
  }

  return false;
}

function WMH_FixZAxis(out pos : Vector) {
  var world : CWorld;
  var z : float;

  world = theGame.GetWorld();

  if (world.NavigationComputeZ(pos, pos.Z - 128, pos.Z + 128, z)) {
    pos.Z = z;
  }
  else if (world.PhysicsCorrectZ(pos, z)) {
    pos.Z = z;
  }
}