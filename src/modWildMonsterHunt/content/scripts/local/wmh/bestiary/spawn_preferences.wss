class WMH_SpawnPreferences {
  private var time_range: WMH_TimeRangeScaling;
  private var biome_tags_mandatory: WMH_BiomeTags;
  private var biome_tags_forbidden: WMH_BiomeTags;

  // A tag unique to this scaling that can be assigned to various POIs and that
  // allows the biome_tags requirements to be ignored
  private var biome_tags_bypass: name;

  public function from(other: WMH_SpawnPreferences): WMH_SpawnPreferences {
    this.time_range = other.time_range;
    this.biome_tags_mandatory = other.biome_tags_mandatory;
    this.biome_tags_forbidden = other.biome_tags_forbidden;
    this.biome_tags_bypass = other.biome_tags_bypass;

    return this;
  }

  public function withTimeRange(range: WMH_TimeRangeScaling): WMH_SpawnPreferences {
    this.time_range = range;

    return this;
  }

  public function withMandatoryBiomeTags(tags: WMH_BiomeTags): WMH_SpawnPreferences {
    this.biome_tags_mandatory = tags;
    
    return this;
  }

  public function withForbiddenBiomeTags(tags: WMH_BiomeTags): WMH_SpawnPreferences {
    this.biome_tags_forbidden = tags;
    
    return this;
  }

  public function withBiomeTagsBypass(tag: name): WMH_SpawnPreferences {
    this.biome_tags_bypass = tag;
    
    return this;
  }

  public function getTimeRangeScaling(hours: float): float {
    if (hours > 20 && hours < 4) {
      return this.time_range.multiplier_night;
    }

    if (hours > 4 && hours < 12) {
      return this.time_range.multiplier_morning;
    }

    return this.time_range.multiplier_afternoon;
  }

  public function isTimeValid(hours: float): bool {
    return this.getTimeRangeScaling(hours) > 0;
  }

  public function isBiomeValid(tags: WMH_BiomeTags): bool {
    if (
      this.biome_tags_forbidden.roomWide && tags.roomWide ||
      this.biome_tags_forbidden.altitudeHigh && tags.altitudeHigh ||
      this.biome_tags_forbidden.humidityHigh && tags.humidityHigh ||
      this.biome_tags_forbidden.vegetationHigh && tags.vegetationHigh ||

      this.biome_tags_forbidden.lightLow && tags.lightLow ||
      this.biome_tags_forbidden.structuresHigh && tags.structuresHigh ||
      this.biome_tags_forbidden.underground && tags.underground
    ) {
      return false;
    }

    if (
      this.biome_tags_mandatory.roomWide && !tags.roomWide ||
      this.biome_tags_mandatory.altitudeHigh && !tags.altitudeHigh ||
      this.biome_tags_mandatory.humidityHigh && !tags.humidityHigh ||
      this.biome_tags_mandatory.vegetationHigh && !tags.vegetationHigh ||

      this.biome_tags_mandatory.lightLow && !tags.lightLow ||
      this.biome_tags_mandatory.structuresHigh && !tags.structuresHigh ||
      this.biome_tags_mandatory.underground && !tags.underground
    ) {
      return false;
    }

    return true;
  }

  public function isBiomeBypassed(biome_tags_bypass: array<name>): bool {
    return IsNameValid(this.biome_tags_bypass)
        && biome_tags_bypass.Contains(this.biome_tags_bypass);
  }
}

struct WMH_TimeRangeScaling {
  // from 20:00 to 04:00
  var multiplier_night: float;

  // from 04:00 to 12:00
  var multiplier_morning: float;

  // from 12:00 to 20:00
  var multiplier_afternoon: float;
}
