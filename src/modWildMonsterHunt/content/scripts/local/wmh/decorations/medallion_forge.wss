class WMH_MedallionForge extends W3Container {
  private var selected_medallion: WMH_Medallion;
  private var prompt: WMH_ForgeYesNoPrompt;

  event OnSpawned(spawnData: SEntitySpawnData)
  {
    super.OnSpawned(spawnData);
    
    // always mark this container as quest container
    SetIsQuestContainer( true );
  }

  event OnInteractionActivated(actionName : string, activator : CEntity) {
    // do not call super as it will probably disable the interaction.
    // super.OnInteractionActivated(actionName, activator);
    this.Enable(true, false, true);
  }

  event OnInteractionActivationTest(
    interactionComponentName: string,
    activator : CEntity
  ) {
    this.Enable(true, false, true);
    return true;
  }

  event OnInteraction(
    actionName : string,
    activator : CEntity
  ) {
    this.startMeldingProcess();
  }

  public function startMeldingProcess() {
    this.promptUserMelding();
  }

  // called after the YES/NO prompt is answered YES
  public function continueMeldingProcess() {
    var trophy: SItemUniqueId = GetWitcherPlayer()
      .GetHorseManager()
      .GetItemInSlot(EES_HorseTrophy);

    var inventory: CInventoryComponent = GetWitcherPlayer()
      .GetHorseManager()
      .GetInventoryComponent();

    if (!inventory.ItemHasTag(trophy, 'WMH_Medallion')) {
      WMHHUD("The trophy you are wearing is not a Medallion that can hold Runes of Power");

      return;
    }

    var reagents_count: int = thePlayer.inv
      .GetItemQuantityByName('modwmh_melding_reagent');

    if (reagents_count < 1) {
      WMHHUD("You lack the necessary Melding Reageants for the process");

      return;
    }

    // the reagent is removed after the item selection

    this.selected_medallion = (new WMH_Medallion in this)
      .init(inventory, trophy);
    this.openItemSelectionMenu();
  }

  timer function finishMeldingProcess(delta: float , id: int) {
    var runes: array<SItemUniqueId> = this.GetInventory().GetItemsByTag('WMH_RuneOfPower');

    var inventory: CInventoryComponent = this.GetInventory();

    for rune: SItemUniqueId in runes {
      this.selected_medallion.appendPerk(WMH_runeGetPerk(inventory, rune));
      thePlayer.inv.RemoveItemByName('modwmh_melding_reagent', 1);
    }

    // consume the runes
    this.GetInventory().RemoveAllItems();
    theSound.SoundEvent("gui_inventory_weapon_attach");
    WMH_updateDecorationPerks();
  }

  private function promptUserMelding() {
    this.prompt = new WMH_ForgeYesNoPrompt in this;
    prompt.forge = this;
    prompt.display();
  }

  private function openItemSelectionMenu() {
    var all_tags: array<name> = GetTags();
    var first_tag: name = all_tags[0];
    
    var item_tags: array<name>;
    item_tags.PushBack('WMH_RuneOfPower');

    WMH_displayItemSelection(
      first_tag,
      this.GetInventory(),
      item_tags,
      true
    );

    AddTimer( 'finishMeldingProcess', 0.1 );
  }
}

class WMH_ForgeYesNoPrompt extends WMH_YesNoPrompt {
  public var forge: WMH_MedallionForge;

  public function title(): string {
    return "Medallion melding";
  }

  public function description(): string {
    return "Use Melding Reagents to meld Runes Of Power onto your currently equipped medallion?";
  }

  public function onResponse(accepted: bool) {
    if (accepted) {
      this.forge.continueMeldingProcess();
    }
  }
}
