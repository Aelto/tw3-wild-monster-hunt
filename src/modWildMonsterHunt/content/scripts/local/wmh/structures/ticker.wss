/// A type to abstract away the concept of cooldowns and ticking.
class WMH_Ticker {
  private var trigger_timestamp: float;
  private var cooldown_seconds: float;

  // when the Ticker is locked, the elapsed time since the last reset is stored
  // so that when unlocked the Ticker starts back exactly to the same duration
  private var lock_elapsed: float;

  public function init(cooldown_seconds: float): WMH_Ticker {
    this.cooldown_seconds = cooldown_seconds;
    this.trigger_timestamp = (cooldown_seconds + 1) * -1;

    return this;
  }

  public function reset(optional cooldown_seconds: float) {
    this.internal_unlock();

    this.trigger_timestamp = SUH_now();
    
    if (cooldown_seconds > 0) {
      this.cooldown_seconds = cooldown_seconds;
    }
  }

  // when set to true the Ticker will never be marked as
  public function lock() {
    this.lock_elapsed = SUH_elapsed(this.trigger_timestamp);
  }

  public function unlock() {
    this.reset();
    this.internal_unlock();
  }

  public function isLocked(): bool {
    return this.lock_elapsed > 0;
  }

  public function hasExpired(): bool {
    // a locked Ticker cannot be considered expired
    return !this.isLocked()
        && SUH_hasElapsed(this.trigger_timestamp, this.cooldown_seconds);
  }

  /// WARNING: mutates the counter if it is expired
  public function validate(): bool {
    if (this.hasExpired()) {
      this.reset();
      return true;
    }
    else {
      return false;
    }
  }

  private function internal_unlock() {
    if (this.isLocked()) {
      this.trigger_timestamp -= this.lock_elapsed;
      this.lock_elapsed = 0;
    }
  }
}

class WMH_TickerGameTime {
  private var trigger_timestamp: GameTime;
  private var cooldown_seconds: int;

  public function init(cooldown_seconds: int): WMH_TickerGameTime {
    this.reset(cooldown_seconds);

    return this;
  }

  public function reset(optional cooldown_seconds: int) {
    this.trigger_timestamp = theGame.CalculateTimePlayed();
    
    if (cooldown_seconds > 0) {
      this.cooldown_seconds = cooldown_seconds;
    }
  }

  /// WARNING: mutates the counter if it is expired
  public function validate(): bool {
    var difference: int = GameTimeToSeconds(theGame.CalculateTimePlayed()) - GameTimeToSeconds(this.trigger_timestamp);

    if (difference >= this.cooldown_seconds) {
      this.reset();
      return true;
    }
    else {
      return false;
    }
  }
}
