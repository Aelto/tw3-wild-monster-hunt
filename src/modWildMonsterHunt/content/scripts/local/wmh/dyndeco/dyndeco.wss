class WMH_DynDecoration extends CGameplayEntity {
  private var decorations: array<CEntity>;

  protected var seed: int;

  protected editable var offset_seed_with_position: bool;
  default offset_seed_with_position = true;
  hint offset_seed_with_position = "if enabled, activates the use of a seed for PRNG and use the entity's position as part of the seed.";

  protected editable var offset_seed_with_hunt: bool;
  default offset_seed_with_hunt = true;
  hint offset_seed_with_hunt = "if enabled, activates the use of a seed for PRNG and use the current WMH hunt seed as part of the seed.";

  event OnSpawned(spawnData: SEntitySpawnData ) {
    super.OnSpawned(spawnData);

    if (this.offset_seed_with_position) {
      var position: Vector = this.GetWorldPosition();

      this.seed += (position.X as int) - (position.Y as int);
    }

    if (this.offset_seed_with_hunt) {
      this.seed += WMH_getHuntManager().getSeed();
    }

    if (!this.offset_seed_with_hunt && this.offset_seed_with_position) {
      this.seed = theGame.GetEngineTimeAsSeconds() + RandRange(10000);
    }

    this.AddTimer('delayedGeneratedRequest', 5.0);
	}

  public function getSeed(optional offset: int): int {
    return this.seed + offset;
  }

  timer function delayedGeneratedRequest(delta: float , id: int) {
    this.requestAsynchronousGeneration();
  }

  public function requestAsynchronousGeneration() {
    thePlayer.wmh_dyndeco_orchestrator.requestGeneration(this);
  }

  public latent function generateDynamicDecoration() {
    var components: array<CComponent> = this
      .GetComponentsByClassName('WMH_DyndecoSlot');

    for component: CComponent in components {
      var slot: WMH_DyndecoSlot = component as WMH_DyndecoSlot;

      if (!slot) {
        continue;
      }

      slot.generateDynamicDecoration();
    }
  }

  public function clearDynamicDecorations() {
    var components: array<CComponent> = this
      .GetComponentsByClassName('WMH_DyndecoSlot');

    for component: CComponent in components {
      var slot: WMH_DyndecoSlot = component as WMH_DyndecoSlot;

      if (!slot) {
        continue;
      }

      slot.clearDynamicDecoration();
    }
  }

  public function beforeDestroy() {}
}

@addField(CR4Player)
var wmh_dyndeco_orchestrator: WMH_DynDecoOrchestrator;

@wrapMethod(CR4Player)
function OnSpawned(spawnData : SEntitySpawnData) {
  wrappedMethod(spawnData);

  if (!this.wmh_dyndeco_orchestrator) {
    this.wmh_dyndeco_orchestrator = new WMH_DynDecoOrchestrator in this;
  }

  this.wmh_dyndeco_orchestrator.onLoad();
}

statemachine class WMH_DynDecoOrchestrator {
  protected var requests_buffers: array<array<WMH_DynDecoration>>;

  protected var buffer_index_opened: int;
  protected var buffer_index_closed: int;

  public function onLoad() {
    this.buffer_index_closed = 1;
    this.buffer_index_opened = 0;
    this.requests_buffers.Grow(2);
    this.GotoState('Waiting');
  }

  public function requestGeneration(object: WMH_DynDecoration) {
    this.requests_buffers[this.buffer_index_opened].PushBack(object);
    
    if (this.GetCurrentStateName() != 'Processing') {
      this.GotoState('Processing');
    }
  }
}

state Waiting in WMH_DynDecoOrchestrator {
  event OnEnterState(previous_state_name: name) {
    super.OnEnterState(previous_state_name);
    this.Waiting_main();
  }

  entry function Waiting_main() {
    if (parent.requests_buffers[parent.buffer_index_opened].Size() > 0) {
      parent.GotoState('Processing');
    }
  }
}

state Processing in WMH_DynDecoOrchestrator {
  event OnEnterState(previous_state_name: name) {
    super.OnEnterState(previous_state_name);
    this.Processing_main();
  }

  entry function Processing_main() {
    Sleep(5.0);

    if (parent.buffer_index_closed == 0) {
      parent.buffer_index_closed = 1;
      parent.buffer_index_opened = 0;
    }
    else {
      parent.buffer_index_closed = 0;
      parent.buffer_index_opened = 1;
    }

    var requests_count: int = parent.requests_buffers[parent.buffer_index_closed].Size();
    var index: int = 0;

    while (requests_count > 0 && index < requests_count) {
      var decoration: WMH_DynDecoration = parent
        .requests_buffers[parent.buffer_index_closed][index];

      decoration.generateDynamicDecoration();
      index += 1;
      
      SleepOneFrame();
    }

    parent.requests_buffers[parent.buffer_index_closed].Clear();
    parent.GotoState('Waiting');
  }
}

exec function dyndeco() {
  var entities: array<CEntity>;
  
  theGame.GetEntitiesByTag('DynDeco', entities);

  for entity: CEntity in entities {
    var dyndeco: WMH_DynDecoration = entity as WMH_DynDecoration;

    if (dyndeco) {
      dyndeco.requestAsynchronousGeneration();
    }
  }
}