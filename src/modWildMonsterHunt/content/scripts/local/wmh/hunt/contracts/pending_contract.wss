struct WMH_PendingContractLocation {
  var spawn_point: WMH_BiomeSpawnPoint;

  // Dynamic, you may leave it empty as it will be defined during gameplay.
  //
  // Defines the available locations for the contract based on all the spawn
  // points available in the level.
  var general_heading: float;

  // Dynamic, stores the location of the previous clues to be used to determine
  // the position of the next clue (as long as it follow the general_heading).
  //
  // matches the index of `available_locations`
  var previous_clue_location: Vector;
}

struct WMH_Contract {
  var level: WMH_Level;

  var created_at_minutes: int;

  // to differentiate otherwise similar contracts that were generated in the
  // same time span.
  var index: int;

  var type: WMH_PendingContractType;
}

enum WMH_PendingContractType {
  WMH_PendingContractType_DEFAULT = 0,
  WMH_PendingContractType_MILESTONE = 1,
}

struct WMH_ContractSpawnInfluence {
  var bestiary_id: WMH_BestiaryId;

  /// A value that can be both positive or negative and that represents how
  /// much % should be added (or removed if negative) to the base ratio.
  ///
  /// No matter the actual spawn rate of a bestiary entry, its base ratio
  /// is 1. Because `ratio * 1` has no effect on the value.
  /// - an influence of 0.1 would turn the base ratio into 1.1, or a flat
  /// increase of 10%.
  /// - an influence of -0.3 would turn the base ratio to 0.7, or a flat decrease of 30%.
  var influence: float;
}

class WMH_PendingContract {
  var contract: WMH_Contract;

  var affix_manager: WMH_AffixManager;
  var spawn_influences: array<WMH_ContractSpawnInfluence>;

  var targets: array<WMH_ContractTarget>;

  var encounter_tag: WMH_EncounterTag;
  default encounter_tag = WMH_EncounterTag_ContractRegular;

  public function init(bestiary: WMH_Bestiary, contract: WMH_Contract, optional do_not_inform_gui: bool): WMH_PendingContract {
    this.contract = contract;

    var targets_count: int = WMH_diminishingReturns(
      this.getContractSeed(),
      1.0 + contract.level.value * 0.01,
      1.0
    );

    while (targets_count > 0) {
      this.targets.PushBack(
        (new WMH_ContractTarget in this).initRandom(
          this.getContractSeed(targets_count),
          contract.level
        )
      );

      targets_count -= 1;
    }

    this.affix_manager = (new WMH_AffixManager in this)
      .init(this.contract.level, this.getContractSeed(1));

    var influences_count: int = WMH_diminishingReturns(
      this.getContractSeed(2),
      MinF(0.25 + contract.level.value * 0.01, 3.0),
      1.0
    );

    while (influences_count > 0) {
      this.spawn_influences.PushBack(
        this.generateRandomSpawnInfluence(
          this.getContractSeed(3 + influences_count * 10),
          contract.level
        )
      );

      influences_count -= 1;
    }

    if (!do_not_inform_gui) {
      WMHHUD("New contract: " + this.toTargetLocNameHtml());
    }

    return this;
  }

  public function onHuntStarted(bestiary: WMH_Bestiary) {
    for target: WMH_ContractTarget in this.targets {
      target.onHuntStarted(bestiary);
    }
  }

  public function onSpawn(bestiary: WMH_Bestiary, hunt_seed: int) {
    this.trySpawningTarget(hunt_seed);
  }

  public function onSpawn_clues(bestiary: WMH_Bestiary, hunt_seed: int) {
    this.trySpawningClues(bestiary, hunt_seed);
  }

  public function isContractExpired(): bool {
    var now: int = WMH_getGameTimeMinutes();

    return now - this.contract.created_at_minutes >= ContractDurationMinutes!;
  }

  public function isTargetted(id: WMH_BestiaryId): bool {
    for target: WMH_ContractTarget in this.targets {
      if (target.isTargetted(id)) {
        return true;
      }
    }

    return false;
  }

  public function getContractSeed(optional offset: int): int {
    return offset
         + (this.contract.level.value as int)
         - this.contract.created_at_minutes
         + this.contract.index;
  }

  public function toTargetLocNameHtml(): string {
    var add_space: bool;

    // note: the affixes are prefixed by a space
    var output: string = this.affix_manager.toLocString();

    for target: WMH_ContractTarget in this.targets {
      if (add_space) {
        output += ", ";
      }

      output += target.toTargetLocNameHtml();
      add_space = true;
    }
    
    return output;
  }

  public function toContractDescriptionHtml(): string {
    var output: string;
    var br: string = "<br/>";


    output += "<font size='24' color='#8a5551'>TARGETS:</font> ";
    output += this.toTargetLocNameHtml() + br;

    output += "<font size='18'>";
    output += "Difficulty level " + this.contract.level.value;
    output += "</font>" + br;

    if (this.affix_manager.has_affix) {
      output += br + br + "<font size='24' color='#8a5551'>AFFIXES:</font> " + this.affix_manager.toLocString() + br;
    }

    if (this.spawn_influences.Size()) {
      output += br + br + "<font size='24' color='#8a5551'>INFLUENCES:</font>" + br;
      output += "<font size='18'>The presence of the target has an influence on the region's ecosystem, the chances to meet the following species during the hunt are altered:</font>";
      output += br;

      var bestiary: WMH_Bestiary = WMH_getBestiary();

      for influence: WMH_ContractSpawnInfluence in this.spawn_influences {
        var bentry: WMH_BestiaryEntry = bestiary.entries[influence.bestiary_id.value];
        var influence_perc: int = Abs(FloorF(influence.influence * 100));
        var sign: string = "<font color='#1c8139' size='22'>+";

        if (influence.influence < 0) {
          sign = "<font color='#cf222e' size='22'>-";
        }

        output += " - " + bentry.getLocName() + ": ";
        output += sign + influence_perc + "%</font>" + br;
      }
    }

    var provider: WMH_RewardProvider = new WMH_RewardProvider in this;
    provider.startGroup("REWARDS").lockGroups(true);

    this.getItemRewards(provider);

    output += br + br + "<font size='24' color='#8a5551'>REWARDS:</font> " + provider.toHtml(thePlayer.GetInventory(), true) + br;

    return output;
  }

  public function getType(): WMH_PendingContractType {
    return WMH_PendingContractType_DEFAULT;
  }

  public function canBeCompleted(
    out killed_creatures: array<WMH_HuntKill>,
    optional only_milestones: bool
  ): bool {
    for target: WMH_ContractTarget in this.targets {
      var was_killed: bool = false;

      var i: int;
      for (i = 0; i < killed_creatures.Size() && !was_killed; i += 1) {
        var tag: WMH_EncounterTag = killed_creatures[i].encounter_tag;

        if (!only_milestones || tag == WMH_EncounterTag_ContractMilestone) {
          if (target.isTargetted(killed_creatures[i].bestiary_id)) {
            was_killed = true;
          }
        }
      }

      if (!was_killed) {
        return false;
      }
    }
    
    return true;
  }

  public function onContractCompleted(provider: WMH_RewardProvider) {
    this.getItemRewards(provider);

    WMH_getLootManager()
      .onPendingContractCompleted(this.getContractSeed());

    var storage: WMH_Storage = WMH_getStorage();
    storage.contract.completed_contracts_history.Insert(0, this.contract);

    while (storage.contract.completed_contracts_history.Size() > 10) {
      storage.contract.completed_contracts_history.PopBack();
    }
  }

  public function getItemRewards(out provider: WMH_RewardProvider) {
    for target: WMH_ContractTarget in this.targets {
      // the entry sets the group name as well
      target.bentry.onContractCompleted(
        provider,
        this.contract,
        this.getContractSeed(10)
      );

      var difficulty: int = theGame.GetDifficultyLevel();

      var money: int = WMH_difficultyScaleDownF(
        target.bentry.strength * 1.5
        // at level 25 the rewards are doubled
        * (1 + this.contract.level.value / 25),

        // decreases it by 10% for every difficulty level
        0.1
      ) as int;

      provider.appendMoney(money);

      var experience: int = WMH_difficultyScaleUpF(
        (35 + target.bentry.strength * 7)
        // at level 100 the rewards are doubled, the XP should scale with
        // contract level, but it shouldn't be too high either:
        * (1 + this.contract.level.value / 100.0),

        // increases it by 5% for every difficulty level
        0.05
      ) as int;

      provider.appendExperience(experience);

      var can_drop_reagent: bool = RandNoiseF(this.getContractSeed(), 1.0)
        < MinF(target.bentry.strength * 0.1, 0.5);

      if (can_drop_reagent) {
        provider.appendItem('modwmh_melding_reagent', 1);
      }
    }
  }

  private function trySpawningTarget(hunt_seed: int) {
    for target: WMH_ContractTarget in this.targets {
      target.trySpawningTarget(hunt_seed, this, this.encounter_tag);
    }
  }

  private function trySpawningClues(bestiary: WMH_Bestiary, hunt_seed: int) {
    for target: WMH_ContractTarget in this.targets {
      target.trySpawningClues(this, bestiary, hunt_seed);
    }
  }

  private function generateRandomSpawnInfluence(
    seed: int,
    level: WMH_Level
  ): WMH_ContractSpawnInfluence {
    var bestiary: WMH_Bestiary = WMH_getBestiary();
    var max_id: WMH_BestiaryId = bestiary.getMaxBestiaryId();

    // the influence's value work in increments of 20%, smaller values would
    // feel too small for any effect.
    var min_influence: float = 1.0;
    var max_influence: float = 1.0 + level.value * 0.04;
    var value: float = FloorF(RandNoiseF(seed + 1, max_influence, min_influence));

    var sign_negative: bool = RandNoiseF(seed + 2, 1.0) < 0.5;
    if (sign_negative) {
      value *= -1;
    }

    return WMH_ContractSpawnInfluence(
      WMH_BestiaryId(RandNoiseF(seed, max_id.value + 1) as int),

      ClampF(
        value * 0.20,
        -0.7,
        1.5
      )
    );
  }
}