/// An event that asks the player to trade items X to get items Y, money, & xp
// in exchange.
class WMH_EventNpc_EventClearPoi extends WMH_EventNpc_BaseEvent {
  private var oneliner: WMH_GameplayOnelinerEntity;
  private var poi: WMH_BiomeSpawnPoint;

  public function init(origin: WMH_EventNpc, seed: int) {
    super.init(origin, seed);

    this.findPointToClear();
  }

  public function description(): string {
    var travel: Vector = this.poi.GetWorldPosition()
                         - this.origin.GetWorldPosition();

    return "Clear area "
         + WMH_vectorToCardinalDescription(travel)
         + " from here";
  }

  public function tryStart() {
    if (this.is_active) {
      return;
    }

    if (!this.findPointToClear()) {
      WMHERROR("WMH_EventNpc_EventClearPoi::tryStart(), failed to find poi to clear");
      return;
    }

    // 1. activate spawn, tell it to force spawn,
    if (!this.canBeCompleted()) {
      this.poi.spawnPriorityForce();
      WMH_getBestiary().onEventClearAreaStart(this, this.poi, this.seed);
      
      if (!this.oneliner) {
        this.oneliner = WMH_gameplayOnelinerEntity(
          StrReplace(
            GetLocStringByKey("wmh_event_clear_area_oneliner"),
            "{{icon}}",
            "<img src='img://icons/quests/monsterhunt.png' vspace='-24' />"
          ),
          this.poi
        );

        this.oneliner.offset = Vector(0, 0, 2.0);
      }
    }
    else {
      WMHINFO("WMH_EventNpc_EventClearPoi::tryStart(), can already be completed");
    }

    super.tryStart();
  }

  public function tryComplete(provider: WMH_RewardProvider): bool {
    if (!this.canBeCompleted()) {
      return false;
    }

    provider
      .startGroup("Clear POI event")
      .appendItem(this.itemName(0), this.itemQuantity(0))
      .appendExperience(WMH_difficultyScaleUpF(100, 0.25) as int)
      .appendMoney(WMH_difficultyScaleDownF(10, 0.1) as int);

    this.onDestroy();
    return super.tryComplete(provider);
  }

  public function canBeCompleted(): bool {
    if (this.poi) {
      return this.poi.wasKilledSinceStartOfHunt() && super.canBeCompleted();
    }
    else {
      WMHINFO("WMH_EventNpc_EventClearPoi::canBeCompleted(), missing POI");
    }

    return super.canBeCompleted();
  }

  protected function onDestroy() {
    if (this.oneliner) {
      this.oneliner.unregister();
    }
  }

  private function findPointToClear(): bool {
    if (this.poi) {
      return true;
    }

    var entities: array<CGameplayEntity>;

    // directly fetch the points in range instead of relying on the spawn point 
    // manager as its cache could yield undesired results:
    FindGameplayEntitiesInRange(
      entities,
      origin,
      200,
      100, // max results
      'WMH_BiomeSpawnPoint'
    );

    var index: int = RandNoiseF(this.seed, entities.Size()) as int;
    var entity: CGameplayEntity = entities[index];

    this.poi = entity as WMH_BiomeSpawnPoint;
    if (!this.poi) {
      WMHERROR(
        "EventClearPoi: selected point is not an instance of WMH_BiomeSpawnPoint"
      );

      return false;
    }

    return true;
  }
}